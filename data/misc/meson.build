appdir = join_paths(get_option('datadir'), 'applications')
metainfodir = join_paths(get_option('datadir'), 'metainfo')
desktop_utils = find_program('desktop-file-validate', required: false)

if get_option('gtk-frontend')
  if get_option('install-appdata')
    rubirc_appdata = i18n.merge_file(
      input: 'io.github.RUBIRC.appdata.xml.in',
      output: 'io.github.RUBIRC.appdata.xml',
      po_dir: '../../po',
      install: true,
      install_dir: metainfodir
    )

    appstream_util = find_program('appstream-util', required: false)
    if appstream_util.found()
      test('Validate io.github.RUBIRC.appdata.xml', appstream_util,
        args: ['validate-relax', rubirc_appdata]
      )
    endif
  endif

  desktop_conf = configuration_data()
  if dbus_glib_dep.found()
    desktop_conf.set('exec_command', 'rubirc --existing %U')
  else
    desktop_conf.set('exec_command', 'rubirc %U')
  endif

  desktop_file = configure_file(
    input: 'io.github.RUBIRC.desktop.in.in',
    output: 'io.github.RUBIRC.desktop.in',
    configuration: desktop_conf
  )

  rubirc_desktop = i18n.merge_file(
    input: desktop_file,
    output: 'io.github.RUBIRC.desktop',
    po_dir: '../../po',
    type: 'desktop',
    install: true,
    install_dir: appdir
  )

  if desktop_utils.found()
    test('Validate io.github.RUBIRC.desktop', desktop_utils,
      args: [rubirc_desktop]
    )
  endif
endif

if get_option('theme-manager')
  htm_desktop = i18n.merge_file(
    input: 'io.github.RUBIRC.ThemeManager.desktop.in',
    output: 'io.github.RUBIRC.ThemeManager.desktop',
    po_dir: '../../po',
    type: 'desktop',
    install: true,
    install_dir: appdir
  )

  if desktop_utils.found()
    test('Validate io.github.RUBIRC.ThemeManager.desktop', desktop_utils,
      args: [htm_desktop]
    )
  endif

  install_data('io.github.RUBIRC.ThemeManager.xml',
    install_dir: join_paths(get_option('datadir'), 'mime/packages')
  )
endif

if get_option('plugin')
  plugin_metainfo = []

  # FIXME: These should all get translated somewhere
  if get_option('with-checksum')
    plugin_metainfo += [
      ['Checksum', 'Calculates a checksum for all sent and recieved DCC files', 'MIT']
    ]
  endif

  if get_option('with-fishlim')
    plugin_metainfo += [
      ['Fishlim', 'Allows setting a key for encrypted conversations', 'MIT AND GPL-2.0+']
    ]
  endif

  if get_option('with-lua') != 'false'
    plugin_metainfo += [
      ['Lua', 'Provides a scripting interface in Lua', 'MIT']
    ]
  endif

  if get_option('with-perl') != 'false'
    plugin_metainfo += [
      ['Perl', 'Provides a scripting interface in Perl', 'GPL-2.0+']
    ]
  endif

  if get_option('with-python') != 'false'
    plugin_metainfo += [
      ['Python', 'Provides a scripting interface in Python', 'GPL-2.0+']
    ]
  endif

  if get_option('with-sysinfo')
    plugin_metainfo += [
      ['Sysinfo', 'Adds command to display system information', 'GPL-2.0+']
    ]
  endif

  foreach metainfo : plugin_metainfo
    name = metainfo[0]
    conf = configuration_data()
    conf.set('NAME', name)
    conf.set('SUMMARY', metainfo[1])
    conf.set('LICENSE', metainfo[2])

    configure_file(
      input: 'io.github.RUBIRC.Plugin.metainfo.xml.in',
      output: 'io.github.RUBIRC.Plugin.@0@.metainfo.xml'.format(name),
      configuration: conf,
      install_dir: get_option('install-plugin-metainfo') ? metainfodir : '',
    )
  endforeach
endif
